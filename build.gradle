plugins {
    id 'java'
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'dev.ceymikey'
    version = '1.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.2-1'
        compileOnly 'org.projectlombok:lombok:1.18.42'

        annotationProcessor 'org.jetbrains:annotations:26.0.2-1'
        annotationProcessor 'org.projectlombok:lombok:1.18.42'

        testImplementation 'org.junit.platform:junit-platform-launcher:1.14.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'

        testImplementation 'org.mockito:mockito-core:4.11.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:4.11.0'
        testImplementation 'org.mockito:mockito-inline:4.11.0'
    }

    // Configure test task
    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat = "full"
            showStandardStreams = true
        }

        // Force scan for all files
        scanForTestClasses = true
    }

// Create a separate task for integration tests
    tasks.register('integrationTest', Test) {
        description = 'Runs integration tests.'
        group = 'verification'

        useJUnitPlatform {
            includeTags 'integration'
        }

        testLogging {
            events 'passed', 'skipped', 'failed'
        }

        // Only run this task if explicitly called
        onlyIf { project.hasProperty('runIntegrationTests') }
    }

// Add integration tests to check task dependencies if requested
    if (project.hasProperty('runIntegrationTests')) {
        check.dependsOn integrationTest
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

// Publishing configuration
// SKIPPED FOR NOW SINCE MY SERVICE IS OFFLINE
//publishing {
//    repositories {
//        maven {
//            name = "Discord-Toolchain"
//            url = "https://repo.ceymikey.dev/releases"
//            credentials(PasswordCredentials)
//            authentication {
//                basic(BasicAuthentication)
//            }
//        }
//    }
//    publications {
//        maven(MavenPublication) {
//            groupId = "dev.ceymikey"
//            artifactId = "discord-toolchain"
//            version = project.version
//            from components.java
//        }
//    }
//}