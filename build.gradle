plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'dev.ceymikey'
version = '1.0-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.1'
        compileOnly 'org.projectlombok:lombok:1.18.36'

        implementation 'com.konghq:unirest-java:3.14.5'

        annotationProcessor 'org.jetbrains:annotations:26.0.1'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
        testImplementation 'org.mockito:mockito-core:5.12.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
    }
}

// Configure test task
test {
    useJUnitPlatform {
        // Exclude integration tests by default
        excludeTags 'integration'
    }

    reports {
        html.required = true
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

// Create a separate task for integration tests
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'integration'
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
    }

    // Only run this task if explicitly called
    onlyIf { project.hasProperty('runIntegrationTests') }
}

// Add integration tests to check task dependencies if requested
if (project.hasProperty('runIntegrationTests')) {
    check.dependsOn integrationTest
}